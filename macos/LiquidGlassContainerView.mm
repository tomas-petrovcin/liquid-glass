#import "LiquidGlassContainerView.h"

#import <react/renderer/components/LiquidGlassViewSpec/ComponentDescriptors.h>
#import <react/renderer/components/LiquidGlassViewSpec/EventEmitters.h>
#import <react/renderer/components/LiquidGlassViewSpec/Props.h>
#import <react/renderer/components/LiquidGlassViewSpec/RCTComponentViewHelpers.h>

#import "RCTFabricComponentsPlugins.h"

#if __has_include("LiquidGlass/LiquidGlass-Swift.h")
#import "LiquidGlass/LiquidGlass-Swift.h"
#else
#import "LiquidGlass-Swift.h"
#endif

using namespace facebook::react;

@interface LiquidGlassContainerView () <RCTLiquidGlassContainerViewViewProtocol>
@end

@implementation LiquidGlassContainerView {
  LiquidGlassConatinerViewImpl * _view;
}

+ (ComponentDescriptorProvider)componentDescriptorProvider
{
  return concreteComponentDescriptorProvider<LiquidGlassContainerViewComponentDescriptor>();
}

- (instancetype)initWithFrame:(CGRect)frame
{
  if (self = [super initWithFrame:frame]) {
    static const auto defaultProps = std::make_shared<const LiquidGlassContainerViewProps>();
    _props = defaultProps;

    _view = [[LiquidGlassConatinerViewImpl alloc] init];

    self.contentView = _view;
  }

  return self;
}

- (void)updateProps:(Props::Shared const &)props oldProps:(Props::Shared const &)oldProps
{
  const auto &oldViewProps = *std::static_pointer_cast<LiquidGlassContainerViewProps const>(_props);
  const auto &newViewProps = *std::static_pointer_cast<LiquidGlassContainerViewProps const>(props);

  if (oldViewProps.spacing != newViewProps.spacing) {
    [_view setSpacing:newViewProps.spacing];
  }

  [super updateProps:props oldProps:oldProps];
}

- (void)mountChildComponentView:(NSView<RCTComponentViewProtocol> *)childComponentView index:(NSInteger)index {
  [_view.contentView addSubview:childComponentView positioned:NSWindowAbove relativeTo:nil];
}

- (void)unmountChildComponentView:(NSView<RCTComponentViewProtocol> *)childComponentView index:(NSInteger)index {
  [childComponentView removeFromSuperview];
}

@end
